import categories.colimit_lemmas
import homotopy_theory.formal.cylinder.hep

import .category
import .cylinder
import .homeomorphism
import .inter_union
import .smush

noncomputable theory

open set

open categories
local notation f ` âˆ˜ `:80 g:80 := g â‰« f

namespace homotopy_theory.topological_spaces
open Top
local notation `Top` := Top.{0}

structure pair :=
(space : Top)
(subset : set space)

variables (P Q R : pair)
-- TODO: Is this too weird?
local notation `X` := P.space
local notation `A` := P.subset
local notation `Y` := Q.space
local notation `B` := Q.subset
local notation `Z` := R.space
local notation `C` := R.subset

-- The subspace component of a pair, considered as a space.
def pair.subspace := Top.mk_ob A

local notation `A'` := P.subspace
local notation `B'` := Q.subspace

-- The inclusion of the subspace, considered as a morphism of Top.
@[reducible] def pair.incl : A' âŸ¶ X := incl A

section homeomorphism

def Top.homeomorphism.of_pairs (h : homeomorphism X Y) : Prop := A = h â»Â¹' B
structure pair.homeomorphism :=
(h : homeomorphism X Y)
(is_of_pairs : h.of_pairs P Q)

notation P ` â‰…â‚š ` Q := pair.homeomorphism P Q

variables {P Q R}
include P Q

def pair.homeomorphism.is_of_pairs' (h : pair.homeomorphism P Q) : A = h.h.equiv â»Â¹' B :=
h.is_of_pairs

def pair.homeomorphism.on_subspaces (h : P â‰…â‚š Q) : homeomorphism A' B' :=
h.h.restrict h.is_of_pairs

@[symm] def pair.homeomorphism.symm (h : P â‰…â‚š Q) : Q â‰…â‚š P :=
pair.homeomorphism.mk h.h.symm $
  show B = h.h.equiv.symm â»Â¹' A, from
  by rw [h.is_of_pairs', â†preimage_comp]; simp [preimage_id]

include R

@[trans] def pair.homeomorphism.trans (hâ‚ : P â‰…â‚š Q) (hâ‚‚ : Q â‰…â‚š R) : P â‰…â‚š R :=
pair.homeomorphism.mk (hâ‚.h.trans hâ‚‚.h) $
  show A = (function.comp hâ‚‚.h.equiv hâ‚.h.equiv) â»Â¹' C, from
  by rw [preimage_comp, hâ‚.is_of_pairs', hâ‚‚.is_of_pairs']

end homeomorphism

section prod

def pair.prod : pair :=
pair.mk (Top.prod X Y) {p | p.1 âˆˆ A âˆ¨ p.2 âˆˆ B}

notation P ` âŠ— `:35 Q:34 := pair.prod P Q

variables {P Q R}
include P Q

lemma pair.prod.is_closed (ha : is_closed A) (hb : is_closed B) :
  is_closed (P âŠ— Q).subset :=
is_closed_union
   (continuous_iff_is_closed.mp continuous_fst _ ha)
   (continuous_iff_is_closed.mp continuous_snd _ hb)

lemma prod_comm_is_of_pairs : prod_comm.of_pairs (P âŠ— Q) (Q âŠ— P) :=
by ext pq; cases pq; exact or.comm

def pair.prod_comm : P âŠ— Q â‰…â‚š Q âŠ— P :=
pair.homeomorphism.mk prod_comm prod_comm_is_of_pairs

include R

lemma prod_assoc_is_of_pairs : prod_assoc.of_pairs ((P âŠ— Q) âŠ— R) (P âŠ— (Q âŠ— R)) :=
by ext pqr; rcases pqr with âŸ¨âŸ¨p, qâŸ©, râŸ©; exact or.assoc

def pair.prod_assoc : (P âŠ— Q) âŠ— R â‰…â‚š P âŠ— (Q âŠ— R) :=
pair.homeomorphism.mk prod_assoc prod_assoc_is_of_pairs

-- Maybe we should have made `pair` a category
def pair.prod.congr_right (h : Q â‰…â‚š R) : P âŠ— Q â‰…â‚š P âŠ— R :=
pair.homeomorphism.mk
  { morphism := Top.prod_maps 1 h.h,
    inverse := Top.prod_maps 1 h.h.symm,
    witness_1 := begin
      ext pq, cases pq with p q,
      change (p, h.h.equiv.symm (h.h.equiv q)) = (p, q),
      simp
    end,
    witness_2 := begin
      ext pr, cases pr with p r,
      change (p, h.h.equiv (h.h.equiv.symm r)) = (p, r),
      simp
    end}
  begin
    ext pq, cases pq with p q,
    change p âˆˆ A âˆ¨ q âˆˆ B â†” p âˆˆ A âˆ¨ q âˆˆ h.h.equiv â»Â¹' C,
    rw h.is_of_pairs'
  end

end prod

section pushout

/-

If A and B are closed, then there is a pushout square

  A Ã— B â†’ X Ã— B
    â†“       â†“
  A Ã— Y â†’ (P âŠ— Q).subspace = A Ã— Y âˆª X Ã— B.

Note that A Ã— B here denotes the product of the (sub)spaces A and B,
not the subspace of X Ã— Y on the product of the subsets A and B; and
the same for A Ã— Y and X Ã— B.

-/

variables (ha : is_closed A) (hb : is_closed B)

-- TODO: product bifunctor
protected def pair.iâ‚€ : Top.prod A' B' âŸ¶ Top.prod A' Y :=
Top.mk_hom (Î» p, (p.1, p.2.val)) (by continuity)

protected def pair.iâ‚ : Top.prod A' B' âŸ¶ Top.prod X B' :=
Top.mk_hom (Î» p, (p.1.val, p.2)) (by continuity)

protected def pair.jâ‚€ : Top.prod A' Y âŸ¶ (P âŠ— Q).subspace :=
Top.mk_hom (Î» p, âŸ¨(p.1.val, p.2), or.inl p.1.propertyâŸ©)
  (by continuity)

protected def pair.jâ‚ : Top.prod X B' âŸ¶ (P âŠ— Q).subspace :=
Top.mk_hom (Î» p, âŸ¨(p.1, p.2.val), or.inr p.2.propertyâŸ©)
  (by continuity)

local notation `XY` := Top.prod X Y

-- Establish an isomorphism to the intersection-union pushout square
-- of subspaces of X Ã— Y.
protected def pair.k : homeomorphism (Top.prod A' B') (Top.mk_ob {p : XY | p.1 âˆˆ A âˆ§ p.2 âˆˆ B}) :=
{ morphism :=
    Top.mk_hom
      (Î» p, âŸ¨(p.1.val, p.2.val), âŸ¨p.1.property, p.2.propertyâŸ©âŸ©)
      (by continuity),
  inverse :=
    Top.mk_hom
      (Î» p, (âŸ¨p.val.1, p.property.leftâŸ©, âŸ¨p.val.2, p.property.rightâŸ©))
      (by continuity),
  witness_1 := by ext p; rcases p with âŸ¨âŸ¨a, haâŸ©, âŸ¨b, hbâŸ©âŸ©; refl,
  witness_2 := by ext p; rcases p with âŸ¨âŸ¨a, bâŸ©, âŸ¨ha, hbâŸ©âŸ©; refl }

protected def pair.l1 : homeomorphism (Top.prod A' Y) (Top.mk_ob {p : XY | p.1 âˆˆ A}) :=
{ morphism := Top.mk_hom (Î» p, âŸ¨(p.1.val, p.2), p.1.propertyâŸ©) (by continuity),
  inverse := Top.mk_hom (Î» p, (âŸ¨p.val.1, p.propertyâŸ©, p.val.2)) (by continuity),
  witness_1 := by ext p; rcases p with âŸ¨âŸ¨a, haâŸ©, yâŸ©; refl,
  witness_2 := by ext p; rcases p with âŸ¨âŸ¨a, yâŸ©, haâŸ©; refl }

protected def pair.l2 : homeomorphism (Top.prod X B') (Top.mk_ob {p : XY | p.2 âˆˆ B}) :=
{ morphism := Top.mk_hom (Î» p, âŸ¨(p.1, p.2.val), p.2.propertyâŸ©) (by continuity),
  inverse := Top.mk_hom (Î» p, (p.val.1, âŸ¨p.val.2, p.propertyâŸ©)) (by continuity),
  witness_1 := by ext p; rcases p with âŸ¨x, âŸ¨b, hbâŸ©âŸ©; refl,
  witness_2 := by ext p; rcases p with âŸ¨âŸ¨x, bâŸ©, hbâŸ©; refl }

protected def pair.po :
  Is_pushout (pair.iâ‚€ P Q) (pair.iâ‚ P Q) (pair.jâ‚€ P Q) (pair.jâ‚ P Q) :=
Is_pushout_of_isomorphic
  (@Is_pushout_inter_union (Top.prod X Y) _ _
    (continuous_iff_is_closed.mp continuous_fst _ ha)
    (continuous_iff_is_closed.mp continuous_snd _ hb))
  (pair.iâ‚€ P Q) (pair.iâ‚ P Q)
  (pair.k P Q) (pair.l1 P Q) (pair.l2 P Q) (by funext; refl) (by funext; refl)

end pushout

section interval

def I_0 : pair := pair.mk I01 {0}
instance I_0.subspace.has_zero : has_zero I_0.subspace :=
âŸ¨âŸ¨(0 : I01), mem_singleton _âŸ©âŸ©

def I_0.subspace.singleton : * â‰ƒ I_0.subspace :=
{ to_fun := Î» _, 0,
  inv_fun := Î» _, punit.star,
  left_inv := Î» âŸ¨âŸ©, rfl,
  right_inv := Î» z, show 0 = z, from subtype.eq (mem_singleton_iff.mp z.property).symm }

end interval

section cofibered

open homotopy_theory.cylinder
local notation `i` := i.{1 0}

-- A pair is cofibered if the inclusion of the subspace is a
-- cofibration.
def pair.cofibered : Prop := hep 0 P.incl

def pair.admits_retract : Prop := âˆƒ r : X âŸ¶ A', r âˆ˜ P.incl = 1

-- A pair (X, A) is cofibered if and only if the inclusion map of the
-- pair (X Ã— I, A Ã— I âˆª X Ã— {0}) admits a retract.
--
-- This result holds even without the assumption that A is closed; see
-- [StrÃ¸m, Note on Cofibrations II, Theorem 2]. However, a more
-- intricate argument is then needed to show that A Ã— I âˆª X Ã— {0} is a
-- pushout when (X, A) is cofibered.
lemma pair.cofibered_iff (ha : is_closed A) :
  P.cofibered â†” (P âŠ— I_0).admits_retract :=
have po : _ := pair.po P I_0 ha (is_closed_singleton : is_closed (_ : set I01)),
have po' : _ :=
  Is_pushout_of_isomorphic po
    (i 0 @> P.subspace) P.incl
    (prod_singleton I_0.subspace.singleton)
    (homeomorphism.refl _)
    (prod_singleton I_0.subspace.singleton)
    (by ext; refl) (by ext; refl),
iff.trans (homotopy_theory.cylinder.hep_iff_pushout_retract 0 po'.transpose) $ begin
  have : pair.incl (P âŠ— I_0) = po'.transpose.induced (i 0 @> X) (I &> pair.incl P) _, {
    apply po'.uniqueness,
    { rw [Is_pushout.induced_commutesâ‚], refl },
    { rw [Is_pushout.induced_commutesâ‚€], refl },
  },
  unfold pair.admits_retract, rw this, refl
end

variables {P Q}
lemma admits_retract_congr (h : pair.homeomorphism P Q) :
  P.admits_retract â†’ Q.admits_retract :=
assume âŸ¨r, hrâŸ©,
âŸ¨h.on_subspaces.morphism âˆ˜ r âˆ˜ h.h.inverse, calc
  h.on_subspaces.morphism âˆ˜ r âˆ˜ h.h.inverse âˆ˜ Q.incl
    = h.on_subspaces.morphism âˆ˜ r âˆ˜ h.h.inverse âˆ˜
      (Q.incl âˆ˜ h.on_subspaces.morphism) âˆ˜ h.on_subspaces.inverse      : by simp
... = h.on_subspaces.morphism âˆ˜ (r âˆ˜ P.incl) âˆ˜ h.on_subspaces.inverse
    : by simp [pair.homeomorphism.on_subspaces, homeomorphism.restriction_commutes]
... = ğŸ™ _  : by rw hr; simpâŸ©

lemma prod_empty_admits_retract (K : Top) :
  P.admits_retract â†’ (P âŠ— pair.mk K âˆ…).admits_retract :=
assume âŸ¨r, hrâŸ©,
let r' : Top.prod X K âŸ¶ (P âŠ— pair.mk K âˆ…).subspace :=
  pair.jâ‚€ P (pair.mk K âˆ…) âˆ˜ Top.prod_maps r 1 in
begin
  existsi r',
  ext p, rcases p with âŸ¨âŸ¨a, kâŸ©, h|âŸ¨âŸ¨âŸ©âŸ©âŸ©,
  apply subtype.eq,
  change ((r a).val, k) = (a, k), congr,
  exact congr_arg subtype.val (@@Top.hom_congr hr âŸ¨a, hâŸ©),
end

-- A condition for the product of closed pairs to be
-- cofibered. Actually, P and Q only need to be cofibered (and only
-- one of them needs to be closed); see [StrÃ¸m, Note on Cofibrations
-- II, Theorem 6]. The argument is more intricate and the statement
-- below will suffice for our purposes. We'll show that (Dâ¿, Sâ¿â»Â¹)
-- satisfies the hypothesis on Q.
lemma prod_cofibered (ha : is_closed A) (hb : is_closed B)
  (hq : Q âŠ— I_0 â‰…â‚š pair.mk Y âˆ… âŠ— I_0) :
  P.cofibered â†’ (P âŠ— Q).cofibered :=
let Q' := pair.mk Y âˆ… in
have _ := calc
  (P âŠ— I_0) âŠ— Q'
    â‰…â‚š P âŠ— (I_0 âŠ— Q')  : pair.prod_assoc
... â‰…â‚š P âŠ— (Q' âŠ— I_0)  : pair.prod.congr_right pair.prod_comm
... â‰…â‚š P âŠ— (Q âŠ— I_0)   : pair.prod.congr_right hq.symm
... â‰…â‚š (P âŠ— Q) âŠ— I_0   : pair.prod_assoc.symm,
calc
  P.cofibered
    â†’ (P âŠ— I_0).admits_retract         : (pair.cofibered_iff P ha).mp
... â†’ ((P âŠ— I_0) âŠ— Q').admits_retract  : prod_empty_admits_retract _
... â†’ ((P âŠ— Q) âŠ— I_0).admits_retract   : admits_retract_congr this
... â†’ (P âŠ— Q).cofibered  : (pair.cofibered_iff _ (pair.prod.is_closed ha hb)).mpr

section smush

variables (V : Type) [topological_space V] [smush.admissible' V]

def unit_disk : Top :=
Top.mk_ob (smush.unit_disk V)

def unit_disk_sphere : pair :=
pair.mk (unit_disk V) {v | smush.admissible.norm v.val = (1 : â„)}

def smush : unit_disk_sphere V âŠ— I_0 â‰…â‚š pair.mk (unit_disk V) âˆ… âŠ— I_0 :=
pair.homeomorphism.mk
  (homeomorphism.of_equiv (smush.H_equiv V)
    (smush.continuous_H V) (smush.continuous_vHv V))
  (begin
    change {p : unit_disk V Ã— I01 | _ âˆ¨ p.2 âˆˆ ({0} : set I01)} =
      (smush.H V) â»Â¹' {p : unit_disk V Ã— I01 | p.1 âˆˆ âˆ… âˆ¨ p.2 âˆˆ ({0} : set I01)},
    convert smush.Ht0 V;
    { ext p, change _ âˆ¨ _ â†” _ âˆ¨ _, apply or_congr (iff.refl _),
      rw mem_singleton_iff, apply subtype.ext },
  end)

lemma prod_disk_sphere_cofibered (ha : is_closed A) :
  P.cofibered â†’ (P âŠ— unit_disk_sphere V).cofibered :=
prod_cofibered P _ ha (is_closed_eq (by continuity) continuous_const) (smush V)

end smush

end cofibered

end homotopy_theory.topological_spaces
