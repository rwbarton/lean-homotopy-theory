import category_theory.category
import data.is_equiv
import data.bij_on

/-

The basic finite colimit notions: initial objects, (binary)
coproducts, coequalizers and pushouts.

For each notion X, we define three structures/classes:

* `Is_X`, parameterized by a cocone diagram (but not on the
  commutativity condition!), which describes the commutativity and the
  universal property of the cocone. For example, a value of type
  `Is_pushout f‚ÇÄ f‚ÇÅ g‚ÇÄ g‚ÇÅ` represents the property that the square
  formed by the four morphisms commutes and is a pushout square.

  Each `Is_X` is a subsingleton, but not a Prop, because we want to be
  able to constructively obtain the morphisms whose existence is
  guaranteed by the universal property.

* `X`, parameterized by the "input diagram" for the corresponding sort
  of colimit, which contains the data of a colimit of that sort on
  that diagram. For example, `pushout f‚ÇÄ f‚ÇÅ` consists of an object and
  two maps which form a pushout square together with the given maps f‚ÇÄ
  and f‚ÇÅ. These structures are not subsingletons, because colimits are
  unique only up to unique isomorphism. This structure exists mostly
  to be used in the corresponding `has_X` class.

* `has_X`, a class for categories, which contains the data of a choice
  of `X` for each possible "input diagram". Thus an instance of
  `has_pushouts` is a category with a specified choice of pushout
  cocone on each span. It is not a subsingleton for the same reason
  that `X` is not one.

The `has_X` classes are of obvious importance, but the `Is_X`
structures are also very useful, especially in settings where colimits
of type X are not known to exist a priori. For example, preservation
of colimits by functors (in the usual "up to isomorphism" sense, not
strictly preserving the chosen colimits) is naturally formulated in
terms of `Is_X`, as is the standard fact about gluing together pushout
squares.

We define the `Is_X` structures by directly encoding the universal
property using the `Is_equiv` type, a constructive version of
`bijective`. Not sure yet whether this has advantages. In any case we
provide an alternate "first-order" interface through `mk'`
constructors and lemmas.

-/

-- TODO: "first-order" lemmas for the other notions besides coequalizers

open category_theory.category
local notation f ` ‚àò `:80 g:80 := g ‚â´ f

namespace category_theory
section

universes v u
parameters {C : Type u} [cat : category.{v+1} C]
include cat


section initial_object

section Is
-- The putative initial object.
parameters {a : C}

-- An object is initial if for each x, the Hom set `a ‚ü∂ x` is a
-- singleton.
def initial_object_comparison (x : C) : (a ‚ü∂ x) ‚Üí true :=
Œª _, trivial

parameters (a)
-- The (constructive) property of being an initial object.
structure Is_initial_object : Type (max u v) :=
(universal : Œ† x, Is_equiv (initial_object_comparison x))

instance Is_initial_object.subsingleton : subsingleton Is_initial_object :=
‚ü®by intros p p'; cases p; cases p'; congr‚ü©

parameters {a}
-- Alternative verification of being an initial object.
def Is_initial_object.mk'
  (induced : Œ† {x}, a ‚ü∂ x)
  (uniqueness : ‚àÄ {x} (k k' : a ‚ü∂ x), k = k') :
  Is_initial_object :=
{ universal := Œª x,
  { e :=
    { to_fun := initial_object_comparison x,
      inv_fun := Œª p, induced,
      left_inv := assume h, uniqueness _ _,
      right_inv := assume p, rfl },
    h := rfl } }

parameters (H : Is_initial_object)
def Is_initial_object.induced {x} : a ‚ü∂ x :=
(H.universal x).e.symm trivial

def Is_initial_object.uniqueness {x} (k k' : a ‚ü∂ x) : k = k' :=
(H.universal x).bijective.1 rfl

end Is

-- An initial object.
structure initial_object :=
(ob : C)
(is_initial_object : Is_initial_object ob)

parameters (C)
-- A choice of initial object. (This is basically the same as
-- initial_object, since there are no "parameters" in the definition
-- of an initial object.)
class has_initial_object :=
(initial_object : initial_object)

end initial_object


section coproduct

section Is
-- The putative coproduct diagram.
parameters {a‚ÇÄ a‚ÇÅ b : C} {f‚ÇÄ : a‚ÇÄ ‚ü∂ b} {f‚ÇÅ : a‚ÇÅ ‚ü∂ b}

-- The diagram above is a coproduct diagram when giving a map b ‚ü∂ x is
-- the same as giving a map a‚ÇÄ ‚ü∂ x and a map a‚ÇÅ ‚ü∂ x.
def coproduct_comparison (x : C) : (b ‚ü∂ x) ‚Üí (a‚ÇÄ ‚ü∂ x) √ó (a‚ÇÅ ‚ü∂ x) :=
Œª k, (k ‚àò f‚ÇÄ, k ‚àò f‚ÇÅ)

parameters (f‚ÇÄ f‚ÇÅ)
-- The (constructive) property of being a coproduct.
structure Is_coproduct : Type (max u v) :=
(universal : Œ† x, Is_equiv (coproduct_comparison x))

instance Is_coproduct.subsingleton : subsingleton Is_coproduct :=
‚ü®by intros p p'; cases p; cases p'; congr‚ü©

parameters {f‚ÇÄ f‚ÇÅ}
-- Alternative verification of being a coproduct.
def Is_coproduct.mk'
  (induced : Œ† {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x), b ‚ü∂ x)
  (induced_commutes‚ÇÄ : ‚àÄ {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x), induced h‚ÇÄ h‚ÇÅ ‚àò f‚ÇÄ = h‚ÇÄ)
  (induced_commutes‚ÇÅ : ‚àÄ {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x), induced h‚ÇÄ h‚ÇÅ ‚àò f‚ÇÅ = h‚ÇÅ)
  (uniqueness : ‚àÄ {x} (k k' : b ‚ü∂ x), k ‚àò f‚ÇÄ = k' ‚àò f‚ÇÄ ‚Üí k ‚àò f‚ÇÅ = k' ‚àò f‚ÇÅ ‚Üí k = k') :
  Is_coproduct :=
{ universal := Œª x,
  { e :=
    { to_fun := coproduct_comparison x,
      inv_fun := Œª p, induced p.1 p.2,
      left_inv := assume h, by
        apply uniqueness; rw induced_commutes‚ÇÄ <|> rw induced_commutes‚ÇÅ; refl,
      right_inv := assume p, prod.ext
        (induced_commutes‚ÇÄ p.1 p.2)
        (induced_commutes‚ÇÅ p.1 p.2) },
    h := rfl } }

parameters (H : Is_coproduct)
def Is_coproduct.induced {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x) : b ‚ü∂ x :=
(H.universal x).e.inv_fun (h‚ÇÄ, h‚ÇÅ)

@[simp] lemma Is_coproduct.induced_commutes‚ÇÄ {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x) :
  H.induced h‚ÇÄ h‚ÇÅ ‚àò f‚ÇÄ = h‚ÇÄ :=
congr_arg prod.fst (H.universal x).cancel_right

@[simp] lemma Is_coproduct.induced_commutes‚ÇÅ {x} (h‚ÇÄ : a‚ÇÄ ‚ü∂ x) (h‚ÇÅ : a‚ÇÅ ‚ü∂ x) :
  H.induced h‚ÇÄ h‚ÇÅ ‚àò f‚ÇÅ = h‚ÇÅ :=
congr_arg prod.snd (H.universal x).cancel_right

lemma Is_coproduct.uniqueness {x : C} {k k' : b ‚ü∂ x}
  (e‚ÇÄ : k ‚àò f‚ÇÄ = k' ‚àò f‚ÇÄ) (e‚ÇÅ : k ‚àò f‚ÇÅ = k' ‚àò f‚ÇÅ) : k = k' :=
(H.universal x).bijective.1 (prod.ext e‚ÇÄ e‚ÇÅ)

end Is

-- A coproduct of a given diagram.
structure coproduct (a‚ÇÄ a‚ÇÅ : C) :=
(ob : C)
(map‚ÇÄ : a‚ÇÄ ‚ü∂ ob)
(map‚ÇÅ : a‚ÇÅ ‚ü∂ ob)
(is_coproduct : Is_coproduct map‚ÇÄ map‚ÇÅ)

parameters (C)
-- A choice of coproducts of all diagrams.
class has_coproducts :=
(coproduct : Œ† (a‚ÇÄ a‚ÇÅ : C), coproduct a‚ÇÄ a‚ÇÅ)

end coproduct


section coequalizer

section Is
-- The putative coequalizer diagram. `commutes` is a `variable`
-- because we do not want to index `Is_coequalizer` on it.
parameters {a b c : C} {f‚ÇÄ f‚ÇÅ : a ‚ü∂ b} {g : b ‚ü∂ c}
variables (commutes : g ‚àò f‚ÇÄ = g ‚àò f‚ÇÅ)

-- The diagram above is a coequalizer diagram when giving a map c ‚ü∂ x
-- is the same as giving a map b ‚ü∂ x whose two pullback to a ‚ü∂ x
-- agree.
def coequalizer_comparison (x : C) : (c ‚ü∂ x) ‚Üí {h : b ‚ü∂ x // h ‚àò f‚ÇÄ = h ‚àò f‚ÇÅ} :=
Œª k, ‚ü®k ‚àò g, have _ := commutes, by rw [‚Üêassoc, ‚Üêassoc, this]‚ü©

parameters (f‚ÇÄ f‚ÇÅ g)
-- The (constructive) property of being a coequalizer diagram.
-- TODO: Rewrite this in the same way as Is_pushout, using Bij_on?
structure Is_coequalizer : Type (max u v) :=
(commutes : g ‚àò f‚ÇÄ = g ‚àò f‚ÇÅ)
(universal : Œ† x, Is_equiv (coequalizer_comparison commutes x))

instance Is_coequalizer.subsingleton : subsingleton Is_coequalizer :=
‚ü®by intros p p'; cases p; cases p'; congr‚ü©

parameters {f‚ÇÄ f‚ÇÅ g}
-- Alternative verification of being a coequalizer.
def Is_coequalizer.mk'
  (induced : Œ† {x} (h : b ‚ü∂ x), h ‚àò f‚ÇÄ = h ‚àò f‚ÇÅ ‚Üí (c ‚ü∂ x))
  (induced_commutes : ‚àÄ {x} (h : b ‚ü∂ x) (e), induced h e ‚àò g = h)
  (uniqueness : ‚àÄ {x} (k k' : c ‚ü∂ x), k ‚àò g = k' ‚àò g ‚Üí k = k') :
  Is_coequalizer :=
{ commutes := commutes,
  universal := Œª x,
  { e :=
    { to_fun := coequalizer_comparison commutes x,
      inv_fun := Œª p, induced p.val p.property,
      left_inv := assume h, by apply uniqueness; rw induced_commutes; refl,
      right_inv := assume p, subtype.eq (induced_commutes p.val p.property) },
    h := rfl } }

parameters (H : Is_coequalizer)
def Is_coequalizer.induced {x} (h : b ‚ü∂ x) (e : h ‚àò f‚ÇÄ = h ‚àò f‚ÇÅ) : c ‚ü∂ x :=
(H.universal x).e.symm ‚ü®h, e‚ü©

@[simp] lemma Is_coequalizer.induced_commutes {x} (h : b ‚ü∂ x) (e) :
  H.induced h e ‚àò g = h :=
congr_arg subtype.val $ (H.universal x).cancel_right

lemma Is_coequalizer.uniqueness {x} {k k' : c ‚ü∂ x} (e : k ‚àò g = k' ‚àò g) : k = k' :=
(H.universal x).bijective.1 (subtype.eq e)

end Is

-- A coequalizer of a given diagram.
structure coequalizer {a b : C} (f‚ÇÄ f‚ÇÅ : a ‚ü∂ b) :=
(ob : C)
(map : b ‚ü∂ ob)
(is_coequalizer : Is_coequalizer f‚ÇÄ f‚ÇÅ map)

parameters (C)
-- A choice of coequalizers of all diagrams.
class has_coequalizers :=
(coequalizer : Œ† ‚¶Éa b : C‚¶Ñ (f‚ÇÄ f‚ÇÅ : a ‚ü∂ b), coequalizer f‚ÇÄ f‚ÇÅ)

end coequalizer


section pushout

section Is
-- The putative pushout diagram.
-- TODO: Order of indices? I sometimes prefer the "f g f' g' order",
-- where f' is the pushout of f and g' is the pushout of g; that's
-- "f‚ÇÄ f‚ÇÅ g‚ÇÅ g‚ÇÄ" here.
parameters {a b‚ÇÄ b‚ÇÅ c : C} {f‚ÇÄ : a ‚ü∂ b‚ÇÄ} {f‚ÇÅ : a ‚ü∂ b‚ÇÅ} {g‚ÇÄ : b‚ÇÄ ‚ü∂ c} {g‚ÇÅ : b‚ÇÅ ‚ü∂ c}

-- The diagram above is a pushout diagram when giving a map c ‚ü∂ x is
-- the same as giving a map b‚ÇÄ ‚ü∂ x and a map b‚ÇÅ ‚ü∂ x whose pullbacks to
-- a ‚ü∂ x agree.
def pushout_comparison (commutes : g‚ÇÄ ‚àò f‚ÇÄ = g‚ÇÅ ‚àò f‚ÇÅ) (x : C) :
  (c ‚ü∂ x) ‚Üí {p : (b‚ÇÄ ‚ü∂ x) √ó (b‚ÇÅ ‚ü∂ x) // p.1 ‚àò f‚ÇÄ = p.2 ‚àò f‚ÇÅ} :=
Œª k, ‚ü®(k ‚àò g‚ÇÄ, k ‚àò g‚ÇÅ), have _ := commutes, by rw [‚Üêassoc, ‚Üêassoc, this]‚ü©

parameters (f‚ÇÄ f‚ÇÅ g‚ÇÄ g‚ÇÅ)
-- The (constructive) property of being a pushout.
structure Is_pushout : Type (max u v) :=
(universal : Œ† x,
  Bij_on (Œª (k : c ‚ü∂ x), (k ‚àò g‚ÇÄ, k ‚àò g‚ÇÅ)) set.univ {p | p.1 ‚àò f‚ÇÄ = p.2 ‚àò f‚ÇÅ})

instance Is_pushout.subsingleton : subsingleton Is_pushout :=
‚ü®by intros p p'; cases p; cases p'; congr‚ü©

parameters {f‚ÇÄ f‚ÇÅ g‚ÇÄ g‚ÇÅ}
-- Alternative verification of being a pushout.
def Is_pushout.mk'
  (commutes : g‚ÇÄ ‚àò f‚ÇÄ = g‚ÇÅ ‚àò f‚ÇÅ)
  (induced : Œ† {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x), h‚ÇÄ ‚àò f‚ÇÄ = h‚ÇÅ ‚àò f‚ÇÅ ‚Üí (c ‚ü∂ x))
  (induced_commutes‚ÇÄ : ‚àÄ {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e), induced h‚ÇÄ h‚ÇÅ e ‚àò g‚ÇÄ = h‚ÇÄ)
  (induced_commutes‚ÇÅ : ‚àÄ {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e), induced h‚ÇÄ h‚ÇÅ e ‚àò g‚ÇÅ = h‚ÇÅ)
  (uniqueness : ‚àÄ {x} (k k' : c ‚ü∂ x), k ‚àò g‚ÇÄ = k' ‚àò g‚ÇÄ ‚Üí k ‚àò g‚ÇÅ = k' ‚àò g‚ÇÅ ‚Üí k = k') :
  Is_pushout :=
{ universal := Œª x,
  Bij_on.mk_univ
    { to_fun := pushout_comparison commutes x,
      inv_fun := Œª p, induced p.val.1 p.val.2 p.property,
      left_inv := assume h, by
        apply uniqueness; rw induced_commutes‚ÇÄ <|> rw induced_commutes‚ÇÅ; refl,
      right_inv := assume p, subtype.eq $ prod.ext
        (induced_commutes‚ÇÄ p.val.1 p.val.2 p.property)
        (induced_commutes‚ÇÅ p.val.1 p.val.2 p.property) }
    (assume p, rfl) }

parameters (H : Is_pushout)
include H

def Is_pushout.commutes : g‚ÇÄ ‚àò f‚ÇÄ = g‚ÇÅ ‚àò f‚ÇÅ :=
by convert (H.universal c).maps_to (_ : ùüô c ‚àà set.univ); simp

def Is_pushout.induced {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e : h‚ÇÄ ‚àò f‚ÇÄ = h‚ÇÅ ‚àò f‚ÇÅ) : c ‚ü∂ x :=
(H.universal x).e.symm ‚ü®(h‚ÇÄ, h‚ÇÅ), e‚ü©

private lemma Is_pushout.induced_commutes' {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e) :
  (Œª (k : c ‚ü∂ x), (k ‚àò g‚ÇÄ, k ‚àò g‚ÇÅ)) (H.induced h‚ÇÄ h‚ÇÅ e) = (h‚ÇÄ, h‚ÇÅ) :=
(H.universal x).right_inv ‚ü®(h‚ÇÄ, h‚ÇÅ), e‚ü©

@[simp] lemma Is_pushout.induced_commutes‚ÇÄ {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e) :
  H.induced h‚ÇÄ h‚ÇÅ e ‚àò g‚ÇÄ = h‚ÇÄ :=
congr_arg prod.fst (Is_pushout.induced_commutes' h‚ÇÄ h‚ÇÅ e)

@[simp] lemma Is_pushout.induced_commutes‚ÇÅ {x} (h‚ÇÄ : b‚ÇÄ ‚ü∂ x) (h‚ÇÅ : b‚ÇÅ ‚ü∂ x) (e) :
  H.induced h‚ÇÄ h‚ÇÅ e ‚àò g‚ÇÅ = h‚ÇÅ :=
congr_arg prod.snd (Is_pushout.induced_commutes' h‚ÇÄ h‚ÇÅ e)

lemma Is_pushout.uniqueness {x} {k k' : c ‚ü∂ x}
  (e‚ÇÄ : k ‚àò g‚ÇÄ = k' ‚àò g‚ÇÄ) (e‚ÇÅ : k ‚àò g‚ÇÅ = k' ‚àò g‚ÇÅ) : k = k' :=
(H.universal x).injective (prod.ext e‚ÇÄ e‚ÇÅ)

end Is

-- A pushout of a given diagram.
structure pushout {a b‚ÇÄ b‚ÇÅ : C} (f‚ÇÄ : a ‚ü∂ b‚ÇÄ) (f‚ÇÅ : a ‚ü∂ b‚ÇÅ) :=
(ob : C)
(map‚ÇÄ : b‚ÇÄ ‚ü∂ ob)
(map‚ÇÅ : b‚ÇÅ ‚ü∂ ob)
(is_pushout : Is_pushout f‚ÇÄ f‚ÇÅ map‚ÇÄ map‚ÇÅ)

parameters (C)
-- A choice of pushouts of all diagrams.
class has_pushouts :=
(pushout : Œ† ‚¶Éa b‚ÇÄ b‚ÇÅ : C‚¶Ñ (f‚ÇÄ : a ‚ü∂ b‚ÇÄ) (f‚ÇÅ : a ‚ü∂ b‚ÇÅ), pushout f‚ÇÄ f‚ÇÅ)

end pushout


end
end category_theory
